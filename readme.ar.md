# Json
[[English]](readme.md)

مكتبة لقراءة ملفات جيسون (Json) باستخدام لغة الأسس. تقتصر هذه المكتبة حاليا على قراءة الجيسون و ليس إنشاءه.

## الإضافة إلى المشروع

يمكن إضافة المكتبة للمشروع باستعمال مدير الحزم:

<div dir=rtl>

```
اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/Json"، "جـيسون.أسس")؛
```

</div>

```
import "Apm";
Apm.importFile("Alusus/Json");
```

## مثال

<div dir=rtl>

```
اشمل "مـتم/طـرفية.أسس"؛
اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/Json"، "جـيسون.أسس")؛

استخدم مـتم؛


دالة ابدا {
    // نقوم بتعريف متغير من النمط جـيسون يحمل البيانات التي نريدها
    عرف كائن: جـيسون = "{\n\t\"id\": 1,\n\t\"classes\": [\"c1\", \"c2\", \"c3\"],\n\t\"passed\": true\n}"

    // نقوم بجلب القيمة المخزنة في المفتاح id كمتغير صحيح
    عرف معرف: صحيح = كائن.هات_صحيح("id")؛
    طـرفية.اطبع("id is %d\n", معرف)؛

    // نقوم بالتحقق من أن القيمة المخزنة في المفتاح classes هي مصفوفة
    عرف كائن_الصفوف: جـيسون = كائن.هات_كائن("classes")؛
    إذا كائن_الصفوف.أهي_مصفوفة() {
        طـرفية.اطبع("Array\n")؛
    }
    وإلا {
        طـرفية.اطبع("Not array\n")؛
    }

    // نقوم بجلب القيمة المخزنة في المفتاح passed كمتغير ثنائي
    عرف نجح: ثنائي = كائن.هات_ثنائي("passed")؛
    // نقوم بالتحقق فيما إذا نجح الطالب أم لا
    إذا نجح {
        طـرفية.اطبع("passed\n")؛
    }
    وإلا {
        طـرفية.اطبع("failed\n")؛
    }
}
ابدا()؛
```

</div>

```
import "Srl/Console.alusus"
import "Apm";
Apm.importFile("Alusus/Json");

use Srl
func start {
    // نقوم بتعريف متغير من النمط جـيسون يحمل البيانات التي نريدها
    def obj: Json = "{\n\t\"id\": 1,\n\t\"classes\": [\"c1\", \"c2\", \"c3\"],\n\t\"passed\": true\n}"

    // كمتغير صحيح id نقوم بجلب القيمة المخزنة في المفتاح
    def id: int = obj.getInt("id");
    Console.print("id is %d\n", id);

    // هي مصفوفة classes نقوم بالتحقق من أن القيمة المخزنة في المفتاح
    def classesObj: Json = obj.getObject("classes");
    if (classesObj.isArray()) {
        Console.print("Array\n");
    }
    else {
        Console.print("Not array\n");
    }

    // كمتغير ثنائي passed نقوم بجلب القيمة المخزنة في المفتاح
    def passed: Bool = obj.getBool("passed");
    // نقوم بالتحقق فيما إذا نجح الطالب أم لا
    if (passed) {
        Console.print("passed\n");
    }
    else {
        Console.print("failed\n");
    }

}
start();
```

## الصنف جـيسون (Json)

### المتغيرات

<div dir=rtl>

```
صنف جـيسون {
    عرف نص_جيسون: نص؛
    عرف مفاتيح: مصفوفة[نص]؛
    عرف قيم: مصفوفة[نص]؛
}
```

</div>

```
class Json {
    def jsonString: String;
    def keys: Array[String];
    def values: Array[String];
}
```

`نص_جيسون` (`jsonString`): سلسلة نصية تمثل المعلومات المخزنة كجيسون.

`مفاتيح` (`keys`): المفاتيح التي يحويها نص الجيسون.

`قيم` (`values`) القيم التي تقابل المفاتيح.

### التهيئة

يمكن تهيئة هذا الصنف باستعمال العديد من الطرق:

<div dir=rtl>

```
عملية هذا~هيئ()
```

</div>

```
handler this~init()
```
تهيئة افتراضية بدون أي قيم.

<div dir=rtl>

```
عملية هذا~هيئ(نص: نـص)
```

</div>

```
handler this~init(str: String)
```
تهيئة من سلسلة نصية.

<div dir=rtl>

```
عملية هذا~هيئ(نص: مؤشر[مصفوفة[مـحرف]])
```

</div>

```
handler this~init(str: ptr[array[Char]])
```
تهيئة من مصفوفة من المحارف.

<div dir=rtl>

```
عملية هذا~هيئ(جيسون: سند[جـيسون])
```

</div>

```
handler this~init(json: ref[Json])
```
تهيئة من كائن من هذا الصنف، أي تهيئة ناسخة.


### أهو_كائن (isObject)

<div dir=rtl>

```
عرف أهو_كائن(): ثنائي؛
```

</div>

```
handler this.isObject(): bool;
```
وظيفة تعيد فيما إذا كان نص الجيسون هو عبارة عن كائن أم لا.

### أهي_مصفوفة (isArray)

<div dir=rtl>

```
عرف أهي_مصفوفة(): ثنائي؛
```

</div>

```
handler this.isArray(): bool;
```
وظيفة تعيد فيما إذا كان نص الجيسون هو عبارة عن مصفوفة أم لا.

### هات_الطول (getLength)

<div dir=rtl>

```
عرف هات_الطول(): صحيح؛
```

</div>

```
handler this.getLength(): Int;
```
وظيفة تعيد عدد القيم في نص الجيسون.

### هات (get)

<div dir=rtl>

```
عرف هات(مفتاح: مؤشر[مصفوفة[محرف]]): نص؛
```

</div>

```
handler this.get(key: ptr[array[Char]]): String;
```

وظيفة تعيد القيمة المخزنة في مفتاح.

`مفتاح` (`key`): المفتاح على شكل مصفوفة من المحارف.

<div dir=rtl>

```
عرف هات(دليل: صحيح): نص؛
```

</div>

```
handler this.get(index: Int): String;
```

وظيفة تعيد القيمة ذات الرقم المعطى في `دليل`

`دليل` (`index`): دليل القيمة التي نريدها ضمن مصفوفة القيم.

ملاحظة: في حال كانت القيمة هي سلسلة نصية فيتم إرجاعها مع إشارتي التنصيص.

### هات_نص (getString)

<div dir=rtl>

```
عرف هات_نص(مفتاح: مؤشر[مصفوفة[محرف]]): نص؛
```

</div>

```
handler this.getString(key: ptr[array[Char]]): String;
```

<div dir=rtl>

```
عرف هات_نص(دليل: صحيح): نص؛
```

</div>

```
handler this.getString(index: Int): String
```

الوظيفة هذه بنسختيها تقوم باستدعاء النسخة المناسبة من الوظيفة `هات` وتقوم بإزالة إشارتي التنصيص في حال وجودهما.

### هات_كائن (getObject)

<div dir=rtl>

```
عرف هات_كائن(مفتاح: مؤشر[مصفوفة[محرف]]): جـيسون؛
```

</div>

```
handler this.getObject(key: ptr[array[Char]]): Json;
```

<div dir=rtl>

```
عرف هات_كائن(دليل: صحيح): جـيسون؛
```

</div>

```
handler this.getObject(index: Int): Json;
```

الوظيفة هذه ينسختيها تقوم باستدعاء النسخة المناسبة من الوظيفة `هات` ولكن ترجع القيمة على شكل كائن من الصنف جـيسون.

### هات_صحيح (getInt)

<div dir=rtl>

```
عرف هات_صحيح(مفتاح: مؤشر[مصفوفة[محرف]]): صـحيح[64]؛
```

</div>

```
handler this.getInt(key: ptr[array[Char]]): Int[64];
```

<div dir=rtl>

```
عرف هات_صحيح(دليل: صحيح): صـحيح[64]؛
```

</div>

```
handler this.getInt(index: Int): Int[64];
```

الوظيفة هذه بنسختيها تقوم باستدعاء النسخة المناسبة من الوظيفة `هات` ولكن ترجع القيمة بعد تحويلها إلى النمط `صـحيح[64]`.

### هات_عائم  (getFloat)

<div dir=rtl>

```
عرف هات_عائم(مفتاح: مؤشر[مصفوفة[محرف]]): عـائم[64]؛
```

</div>

```
handler this.getFloat(key: ptr[array[Char]]): Float[64];
```

<div dir=rtl>

```
عرف هات_عائم(دليل: صحيح): عـائم[64]؛
```

</div>

```
handler this.getFloat(index: Int): Float[64];
```

الوظيفة هذه بنسختيها تقوم باستدعاء النسخة المناسبة من الوظيفة `هات` ولكن ترجع القيمة بعد تحويلها إلى النمط `عـائم[64]`.

### هات_ثنائي (getBool)

<div dir=rtl>

```
عرف هات_ثنائي(مفتاح: مؤشر[مصفوفة[محرف]]): ثنائي؛
```

</div>

```
handler this.getBool(key: ptr[array[Char]]): Bool
```

<div dir=rtl>

```
عرف هات_ثنائي(دليل: صحيح): ثنائي؛
```

</div>

```
handler this.getBool(index: Int): Bool;
```

الوظيفة هذه بنسختيها تقوم باستدعاء النسخة المناسبة من الوظيفة `هات` ولكن ترجع القيمة بعد تحويلها إلى النمط `ثنائي`.

